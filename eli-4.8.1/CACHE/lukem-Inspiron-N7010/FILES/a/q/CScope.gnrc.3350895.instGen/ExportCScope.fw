@=~
~p typesetter = texinfo
~p maximum_input_line_length = infinity
~! $Id: CScope.fw,v 5.3 2001/07/24 15:41:17 uwe Exp $
~!
~! (C) Copyright 1997 University of Paderborn.
~! 
~! This file is part of the Eli Module Library.
~! 
~! The Eli Module Library is free software; you can redistribute it and/or
~! modify it under the terms of the GNU Library General Public License as
~! published by the Free Software Foundation; either version 2 of the
~! License, or (at your option) any later version.
~! 
~! The Eli Module Library is distributed in the hope that it will be
~! useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
~! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
~! Library General Public License for more details.
~! 
~! You should have received a copy of the GNU Library General Public
~! License along with the Eli Module Library; see the file COPYING.LIB.
~! If not, write to the Free Software Foundation, Inc., 59 Temple Place -
~! Suite 330, Boston, MA 02111-1307, USA. 
~!


This module implements consistent renaming of identifiers.
Identifier occurrences are bound to object keys of type DefTableKey
according to C-like scope rules:

A binding is valid from the definition upto the end of the smallest 
range containing the definition, except in inner ranges 
from a definition of the same identifier to the end of that range.

A binding is to be understood as a triple (idn, env, k) such that
the identifier idn is bound to the key k in the innermost scope
of the environment env. This module establishes bindings
for defining identifier occurrences and searches bindings for
used identifier occurrences. The results of these operations
are provided as attributes of symbols which represent identifier
occurrences. There is always a pair of such attributes, one
of type Binding, that allows to access the triple information,
and one of type DefTableKey, that is the key component of the
triple. 
(The latter is kept for backward compatibility and
for convenience of use. The describing text below refers only
to the key attribute, it does not mention explicitly that
a binding attribute comes along with it.)

This module is instantiated generically.
If identifiers are to be bound in different name spaces that do not
affect each other, then several instantiations of this module
are to be used.

On instantiation an optional
instance parameter may be supplied. Its value is preceeded to names
used in this module in order to allow several instances of the
module to coincide in one specification. If this text is obtained
from an instantiation the instance parameter is the string enclosed
in the following brackets [Export]. In order to see which names
are affected by the instance parameter you may look at the non-instantiated
module.

On instantiation an optional
referto parameter may be supplied. Its value is preceeded to
Key attribute names in order to allow one symbol representing an
identifier occurrence to be bound in different name spaces.
If that is not the case the referto parameter should not be set
to simplify the use of other library modules that depend on
Key attributes.
If this text is obtained
from an instantiation the referto parameter is the string enclosed
in the following brackets [].

This module provides the following computational roles:

~O~<ExportCScope.lido~>~{
~<ExportRootScope~>
~<ExportRangeScope~>
~<ExportAnyScope~>
~<ExportIdDefScope~>
~<ExportIdUseEnv~>
~<ExportIdUseScope~>
~<ExportIdDefUse~>
~<ExportChkIdUse~>
~<ExportDeclaratorWithId~>
~<ExportIdInDeclarator~>
~}

Usage Overview:

ExportRootScope is inherited to the grammar root by default.
The other SYMBOLs
may be used depending on the particular scope rule to be specified.

The main results of using this module are the bindings of
identifier occurrences represented by the attibutes
ExportIdDefScope.Key, ExportIdUseEnv.Key, and
ExportIdUseScope.Key.
ExportChkIdUse may be used to issue a message if the is no such binding.

Usually both ExportIdDefScope and ExportIdUseEnv are used.
In specific cases of language rules any combination of
ExportIdDefScope, ExportIdUseEnv, ExportIdUseScope
may be used.

The attributes ExportIdDefScope.Sym, ExportIdUseEnv.Sym,
ExportIdUseScope.Sym must represent the identifier encoding.

The Computational Roles:

ExportRootScope is the root symbol containing all identifier occurrences
and all ExportRangeScope.

SYNT.ExportEnv is a fresh root environment where all environments
of this name space are embedded in.
It has the value of a global variable ExportRootEnv that is assigned
in the initialization phase of the processor.
It allows to introduce predefinitions by initialization code.

SYNT.ExportGotKeys indicates that all identifier occurrences
are bound.

~$~<ExportRootScope~>==~{
CLASS SYMBOL ROOTCLASS INHERITS ExportRootScope END;

CHAIN _C_ExportGotKeys: VOID;

CLASS SYMBOL ExportRootScope:
	ExportEnv:	Environment	SYNT,	/* provided */
	ExportGotKeys:		VOID	SYNT;	/* provided */

CLASS SYMBOL ExportRootScope COMPUTE
  SYNT.ExportEnv = ExportRootEnv;
  CHAINSTART HEAD._C_ExportGotKeys = SYNT.ExportEnv;
  SYNT.ExportGotKeys = TAIL._C_ExportGotKeys;
END;
~}

ExportRangeScope
is a symbol representing a range for the binding
of defining identifier occurrences ExportIdDefScope.
It may be nested in ExportRootScope or other ranges.

INH.ExportEnv
is an environment of bindings.
It may be overridden by an upper computation to get one environment
for several ExportRangeScope forming one conceptual range.
If INH.ExportEnv is overridden, the computation of INH.ExportGotKeys must
be overridden accordingly by an upper computation.

SYNT.ExportGotKeys
indicates that all identifer ocurrences from the beginning of the
ExportRootScope up to the end of this range are bound to keys.

~$~<ExportRangeScope~>==~{
CLASS SYMBOL ExportRangeScope:
	ExportEnv:	Environment	INH,	/* provided */
	ExportGotKeys:		VOID	SYNT;	/* provided */

CLASS SYMBOL ExportRangeScope COMPUTE
  INH.ExportEnv = 
	NewScope (INCLUDING ExportAnyScope.ExportEnv);
  SYNT.ExportGotKeys = TAIL._C_ExportGotKeys;
END;
~}

ExportAnyScope 
comprises the roles of ExportRootScope and ExportRangeScope.
It may be used in constructs like INCLUDING ExportAnyScope.GotKeys.

~$~<ExportAnyScope~>==~{
CLASS SYMBOL ExportAnyScope END;
CLASS SYMBOL ExportRootScope	INHERITS ExportAnyScope END;
CLASS SYMBOL ExportRangeScope	INHERITS ExportAnyScope END;
~}

ExportIdDefScope
is a defining identifier occurrence THIS.Sym that is to be defined
in the scope of the immediately enclosing ExportAnyScope.

~$~<ExportIdDefScope~>==~{
CLASS SYMBOL ExportIdDefScope:
	Bind:	Binding		SYNT,	/* provided */
	Key:	DefTableKey	SYNT,	/* provided */
	Sym:		int;			/* required */

CLASS SYMBOL ExportIdDefScope COMPUTE
  SYNT.Bind =
	BindIdn (INCLUDING ExportAnyScope.ExportEnv, THIS.Sym)
	<- THIS._C_ExportGotKeys;
  SYNT.Key = KeyOf (SYNT.Bind);
  THIS._C_ExportGotKeys = SYNT.Key;
END;
~}

ExportIdUseEnv 
is an applied identifier occurrence THIS.Sym that is to be
bound in the scope of the immediately enclosing ExportAnyScope.

~$~<ExportIdUseEnv~>==~{
CLASS SYMBOL ExportIdUseEnv:
	Bind:	Binding		SYNT,	/* provided */
	Key:	DefTableKey	SYNT,	/* provided */
	Sym:		int;			/* required */

CLASS SYMBOL ExportIdUseEnv INHERITS _ExportInhPrecondDefault COMPUTE
  SYNT.Bind =
	BindingInEnv (INCLUDING ExportAnyScope.ExportEnv, THIS.Sym)
	<- (THIS._C_ExportGotKeys, SYNT._ExportInhPrecond);
  SYNT.Key = KeyOf (SYNT.Bind);
  THIS._C_ExportGotKeys = SYNT.Key;
END;

CLASS SYMBOL _ExportInhPrecondDefault:
	_ExportInhPrecond:	VOID	SYNT;   /* local */
CLASS SYMBOL _ExportInhPrecondDefault COMPUTE
  SYNT._ExportInhPrecond = "yes";
END;
~}

ExportIdUseScope
is a symbol representing an applied identifier occurrence
THIS.Sym that is to be bound in the scope of the
smallest enclosing range. Enclosing environments are not considered.

~$~<ExportIdUseScope~>==~{
CLASS SYMBOL ExportIdUseScope:
	Sym:		int,			/* required */
	Bind:	Binding		SYNT,	/* provided */
	Key:	DefTableKey	SYNT;	/* provided */

CLASS SYMBOL ExportIdUseScope INHERITS _ExportInhPrecondDefault COMPUTE
  SYNT.Bind =
	BindingInScope (INCLUDING ExportAnyScope.ExportEnv, THIS.Sym)
	<- (THIS._C_ExportGotKeys, SYNT._ExportInhPrecond);
  SYNT.Key = KeyOf (SYNT.Bind);
  THIS._C_ExportGotKeys = SYNT.Key;
END;
~}

ExportIdDefUse
represents a defining identifier occurrence
like ExportIdDefScope if INH.ExportDefCond is non-zero,
otherwise an applied occurrence like ExportIdUseEnv.
By default INH.ExportDefCond is 1 if there is no binding
for this identifier in the current environment.
INH.ExportDefCond can be overridden by an upper computation.

~$~<ExportIdDefUse~>==~{
CLASS SYMBOL ExportIdDefUse:
	Sym:		int,			/* required */
	ExportDefCond:	int		INH,	/* default */
	Bind:	Binding		SYNT,	/* provided */
	Key:	DefTableKey	SYNT,	/* provided */
	_ExportauxGotKeys: VOID		SYNT;	/* internal */

CLASS SYMBOL ExportIdDefUse INHERITS _ExportInhPrecondDefault COMPUTE
  SYNT._ExportauxGotKeys = THIS._C_ExportGotKeys;
  INH.ExportDefCond =
	EQ (NoBinding, 
	    BindingInEnv (INCLUDING ExportAnyScope.ExportEnv, THIS.Sym))
	<- (THIS._ExportauxGotKeys, SYNT._ExportInhPrecond);
  SYNT.Bind =
	IF (THIS.ExportDefCond,
	BindIdn (INCLUDING ExportAnyScope.ExportEnv, THIS.Sym),
	BindingInEnv (INCLUDING ExportAnyScope.ExportEnv, THIS.Sym))
	<- (THIS._C_ExportGotKeys, SYNT._ExportInhPrecond);
  SYNT.Key = KeyOf (SYNT.Bind);
  THIS._C_ExportGotKeys = SYNT.Key;
END;
~}

ExportChkIdUse
may be used for any applied identifier occurrence to issue an error
message if no binding is found.

~$~<ExportChkIdUse~>==~{
CLASS SYMBOL ExportChkIdUse:
        Key:       DefTableKey     SYNT,   /* required */
        Sym:            int,                    /* required */
        SymErr:         int             SYNT,   /* provided */
        SymMsg:         VOID            SYNT;   /* provided */

CLASS SYMBOL ExportChkIdUse COMPUTE
  SYNT.SymErr = EQ (THIS.Key, NoKey);
  SYNT.SymMsg = IF (THIS.SymErr,
    message (ERROR, CatStrInd ("Identifier is not defined: ", THIS.Sym),
           0, COORDREF));
END;
~}

ExportDeclaratorWithId
is a construct that contains exactly one defining identifier occurrence 
represented by the role ExportIdInDeclarator.
The identifier is bound in the scope of the immediately enclosing 
ExportAnyScope. The binding becomes valid immediately after the end of
this construct.

~$~<ExportDeclaratorWithId~>==~{
CLASS SYMBOL ExportDeclaratorWithId:
	Bind:	Binding		SYNT,	/* provided */
	Key:	DefTableKey	SYNT,	/* provided */
	Sym:		int		SYNT;	/* provided */

CLASS SYMBOL ExportDeclaratorWithId COMPUTE
  SYNT.Sym = CONSTITUENT ExportIdInDeclarator.Sym SHIELD ExportDeclaratorWithId;
  SYNT.Bind =
	BindIdn (INCLUDING ExportAnyScope.ExportEnv, THIS.Sym)
	<- TAIL._C_ExportGotKeys;
  SYNT.Key = KeyOf (SYNT.Bind);
  THIS._C_ExportGotKeys = SYNT.Key;
END;
~}

ExportIdInDeclarator
is a defining identifier occurrence THIS.Sym that is contained in
a ExportDeclaratorWithId construct.

~$~<ExportIdInDeclarator~>==~{
CLASS SYMBOL ExportIdInDeclarator:
	Bind:	Binding		SYNT,	/* provided */
	Key:	DefTableKey	SYNT,	/* provided */
	Sym:		int;			/* required */

CLASS SYMBOL ExportIdInDeclarator COMPUTE
  SYNT.Bind = INCLUDING ExportDeclaratorWithId.Bind;
  SYNT.Key = KeyOf (SYNT.Bind);
END;
~}

The following C module provides the variable ExportRootEnv, which may be
used for initialization of bindings in the root environment,
as done by the predefinition modules.

~O~<ExportCScope.head~>~{
#include "ExportCScope.h"
#include "err.h"
~}

~O~<ExportCScope.h~>~{
#include "envmod.h"
extern Environment ExportRootEnv;
~}

~O~<ExportCScope.c~>~{
#include "ExportCScope.h"
Environment ExportRootEnv = NoEnv;
~}

~O~<ExportCScope.init~>~{
if (ExportRootEnv == NoEnv) ExportRootEnv = NewEnv ();
~}

~O~<ExportCScope.specs~>~{
$/Name/envmod.specs
$/Input/err.specs
$/Tech/Strings.specs
~}

